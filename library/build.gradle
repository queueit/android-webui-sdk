apply plugin: 'com.android.library'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30

        buildConfigField 'String', 'VERSION_NAME', "\"$libraryVersion\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }


    flavorDimensions "androidx"
    productFlavors{
        library_androidx
        library
    }
    lint {
        abortOnError false
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'pl.pragmatists:JUnitParams:1.0.5'
    testImplementation 'org.robolectric:robolectric:4.4'

    implementation 'com.squareup.okhttp3:okhttp:4.9.2'
    library_androidxApi 'androidx.appcompat:appcompat:1.2.0'
    library_androidxApi 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'

    //noinspection GradleCompatible
    libraryImplementation 'com.android.support:appcompat-v7:28.0.0'
}

apply plugin: 'maven-publish'

group = groupId
version = libraryVersion

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

nexusStaging {
    stagingProfileId = "219f8fe296e7e"
    packageGroup = groupId
    username = OSSRH_USERNAME
    password = OSSRH_PASSWORD
}

apply from: '../publish.gradle' // Makes this library publishable